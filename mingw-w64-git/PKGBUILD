
_realname=git
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.44.0.windows.1
pkgrel=2
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://git-for-windows.github.io/"
license=('GPL2')


makedepends=("docbook-xsl"
             "xmlto"
             "${MINGW_PACKAGE_PREFIX}-asciidoc"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-tk")

depends=("${MINGW_PACKAGE_PREFIX}-ca-certificates"
         "${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-expat>=2.0"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "perl>=5.14.0"
         "perl-Authen-SASL"
         "perl-Error"
         "perl-libwww"
         "perl-MIME-tools"
         "perl-Net-SMTP-SSL"
         "perl-TermReadKey")

optdepends=("subversion: git-svn"
            "${MINGW_PACKAGE_PREFIX}-python: git-p4"
            "${MINGW_PACKAGE_PREFIX}-tk: gitk & git-gui")

# These packages are provided by git-sdk by git-for-windows project.
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}-doc-html"
          "${MINGW_PACKAGE_PREFIX}-${_realname}-doc-man")

source=("https://github.com/git-for-windows/git/archive/refs/tags/v$pkgver.tar.gz"
        "0001-fix-create-link-destdir.patch"
        "0002-fix-build-with-mingw32-and-clang.patch"
        "0003-fix-version.patch"
        "0007-use-env-as-interpreter.patch"
        "0008-fix-python-script-shebang.patch"
        "0101-arm-remove-nedmalloc.patch")

sha256sums=('08819e26e29a563f75a3c12910e0b46dce100001bfa42d57395e66a4b7d168a8'
            '5119b9b816a8fc0de5f677bc1aa69b92fe11ecb4ecd4e65b7583aff727cb4222'
            'be17cad4f3ed6bf8185fe35fb77bdc4490bde313406ae35d9544da8a89681d97'
            'dd4a269ffdf9c0e5ea3d921e7fc9309c65babe40916113759950d64cdec468ce'
            '2af342592696fa4c55439634a82ae0c37133e82fcf623f4b70d59a0bb4450cad'
            'bdf4f6026c6ad81620e5b4402873d30ea4bb19fb361f5ec13aff8ea9600ce219'
            '2464016253e6f8f35d27036d7708c779b599350d8d062007ab52a6341df3fd77')

prepare() {
    cd "$srcdir/$_realname-$pkgver"
    patch -Np1 -i $srcdir/0001-fix-create-link-destdir.patch
    patch -Np1 -i $srcdir/0002-fix-build-with-mingw32-and-clang.patch
    patch -Np1 -i $srcdir/0003-fix-version.patch
    patch -Np1 -i $srcdir/0007-use-env-as-interpreter.patch
    patch -Np1 -i $srcdir/0008-fix-python-script-shebang.patch
    if test "CLANGARM64" == "$MSYSTEM"
    then
        patch -Np1 -i $srcdir/0101-arm-remove-nedmalloc.patch
    fi
    echo "v$pkgver-$pkgrel" > version
}

build() {
    # Git wants to decide itself whether to use ANSI stdio emulation or not
    CPPFLAGS="$(echo "$CPPFLAGS" |
        sed 's/-D__USE_MINGW_ANSI_STDIO\(=[0-9]*\)\?//')"

    # run cmake first, it will generate GIT-VERSION-FILE
    mkdir -p $srcdir/build-${MSYSTEM}
    cd $srcdir/build-${MSYSTEM}
    MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=;-DFALLBACK_RUNTIME_PREFIX=" \
        ${MINGW_PREFIX}/bin/cmake.exe \
        $srcdir/$_realname-$pkgver/contrib/buildsystems \
        -GNinja \
        -DUSE_VCPKG=off \
        -DSKIP_DASHED_BUILT_INS=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX="$MINGW_PREFIX" \
        -DFALLBACK_RUNTIME_PREFIX="$MINGW_PREFIX"
    ${MINGW_PREFIX}/bin/cmake --build .
}

package () {
    cd $srcdir/build-${MSYSTEM}
    DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake --install .

    cd "$srcdir/$_realname-$pkgver"

    # gitk
    make -C gitk-git prefix="$pkgdir/$MINGW_PREFIX" install

    # git-gui
    DESTDIR="$pkgdir" make -C git-gui gitexecdir="$MINGW_PREFIX/libexec/git-core" install

    # gitweb
    DESTDIR="$pkgdir" make prefix="$MINGW_PREFIX" gitwebdir="$MINGW_PREFIX/var/www/cgi-bin" install-gitweb

    # docs
    DESTDIR="$pkgdir" make prefix="$MINGW_PREFIX" install-html
    DESTDIR="$pkgdir" make prefix="$MINGW_PREFIX" install-man

    # subtree, for backwards-compatibility with MSys2's Git package
    DESTDIR="$pkgdir" make -C contrib/subtree prefix="$MINGW_PREFIX" install
    DESTDIR="$pkgdir" make -C contrib/subtree prefix="$MINGW_PREFIX" install-doc

    # contacts
    DESTDIR="$pkgdir" make -C contrib/contacts prefix="$MINGW_PREFIX" install
    DESTDIR="$pkgdir" make -C contrib/contacts prefix="$MINGW_PREFIX" install-doc

    # jump
    install contrib/git-jump/git-jump "$pkgdir/$MINGW_PREFIX/libexec/git-core/"

    # resurrect
    install contrib/git-resurrect.sh "$pkgdir/$MINGW_PREFIX/libexec/git-core/git-resurrect"

    # mw
    DESTDIR="$pkgdir" make -C contrib/mw-to-git prefix="$MINGW_PREFIX" perllibdir="$MINGW_PREFIX/share/perl5" install

    # git-credential-wincred.exe
    make -C contrib/credential/wincred prefix="$pkgdir/$MINGW_PREFIX" install

    # completions
    install -d "$pkgdir/$MINGW_PREFIX/share/git/completion/"
    install contrib/completion/* "$pkgdir/$MINGW_PREFIX/share/git/completion/"
}
