# Maintainer: Berrysoft <Strawberry_Str@hotail.com>

_realname=git
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
tag=2.36.1.windows.1
pkgver=2.36.1.1.e2ff68a2d1
# pkgrel should be larger than the official one
pkgrel=4
pkgdesc="The fast distributed version control system (mingw-w64)"
arch=('any')
url="https://git-for-windows.github.io/"
license=('GPL2')

options=('strip')

makedepends=("diffutils"
             "docbook-xsl"
             "git"
             "make"
             "patch"
             "tar"
             "xmlto"
             "${MINGW_PACKAGE_PREFIX}-asciidoc"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-tk")

depends=("${MINGW_PACKAGE_PREFIX}-curl"
         "${MINGW_PACKAGE_PREFIX}-ca-certificates"
         "${MINGW_PACKAGE_PREFIX}-expat>=2.0"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libiconv"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-pcre2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "perl>=5.14.0"
         "perl-Authen-SASL"
         "perl-Error"
         "perl-libwww"
         "perl-MIME-tools"
         "perl-Net-SMTP-SSL"
         "perl-TermReadKey")

optdepends=("mintty"
            "${MINGW_PACKAGE_PREFIX}-python: git-p4 support"
            "${MINGW_PACKAGE_PREFIX}-tk: gitk & git-gui support")

conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}-doc-html"
	       "${MINGW_PACKAGE_PREFIX}-${_realname}-doc-man")

source=("git"::"git+https://github.com/git-for-windows/git.git#tag=v$tag")

patches=("1-mingw.patch"
         "3-pcre2.patch"
         "4-headless.patch"
         "5-no-unicode.patch"
         "6-interpr.patch")
source+=(${patches[@]})

sha256sums=('SKIP'
            '0751948ca85123109e51a4e379411c7d2b94e400c901c44305634aa5036f752e'
            'af2cdf0b91764c1b5c4428681b08a843a2883dfaafa436b57638edc67c4e3efc'
            'b5edee84ac261a554d73deadfe7c538a0c7ebc85b87e6633061138d55de3f93e'
            '0e1bac656e942206e805a50ec3f1c21d5e1ba619c2718800c4f43e142f31f818'
            'f3380f14ed3e02ad6fe60cdb8bfe18eba9555e1b9b897a4806b2419e26cc9b51')

pkgver() {
    cd "$srcdir/git"
    basever=${tag%.windows.*}
    test "$tag" = "$basever" ||
    basever="$basever.${tag##*.windows.}"
    basever="${basever/-/.}"
    printf "%s.%s" "${basever}" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/git"
    if test false != "$(git config core.autocrlf)"
        then
        git config core.autocrlf false
        rm .git/index
        git reset --hard
    fi
    for p in ${patches[@]}
    do
        git apply -v ${srcdir}/$p
    done
}

package () {
    # Git wants to decide itself whether to use ANSI stdio emulation or not
    CPPFLAGS="$(echo "$CPPFLAGS" |
        sed 's/-D__USE_MINGW_ANSI_STDIO\(=[0-9]*\)\?//')"

    cd "$srcdir"/git

    # gitk
    make -C gitk-git prefix="$pkgdir/$MINGW_PREFIX" install

    # git-gui
    DESTDIR="$pkgdir" make -C git-gui gitexecdir="$MINGW_PREFIX/libexec/git-core" install

    # gitweb
    DESTDIR="$pkgdir" make -C gitweb prefix="$MINGW_PREFIX" gitwebdir="$MINGW_PREFIX/var/www/cgi-bin" install

    # docs
    DESTDIR="$pkgdir" make prefix="$MINGW_PREFIX" install-html
    DESTDIR="$pkgdir" make prefix="$MINGW_PREFIX" install-man

    # subtree, for backwards-compatibility with MSys2's Git package
    make -C contrib/subtree prefix="$pkgdir/$MINGW_PREFIX" install
    make -C contrib/subtree prefix="$pkgdir/$MINGW_PREFIX" install-html
    make -C contrib/subtree prefix="$pkgdir/$MINGW_PREFIX" install-man

    # git-credential-wincred.exe
    make -C contrib/credential/wincred prefix="$pkgdir/$MINGW_PREFIX" install

    # completions
    install -d "$pkgdir/$MINGW_PREFIX/share/git/completion/"
    install contrib/completion/* "$pkgdir/$MINGW_PREFIX/share/git/completion/"

    # git
    [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
    cd ${srcdir}/build
    MSYS2_ARG_CONV_EXCL="-DFALLBACK_RUNTIME_PREFIX=" ${MINGW_PREFIX}/bin/cmake.exe ${srcdir}/git/contrib/buildsystems \
        -GNinja \
        -DUSE_VCPKG=off \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX="$pkgdir/$MINGW_PREFIX" \
        -DFALLBACK_RUNTIME_PREFIX="$MINGW_PREFIX"
    ${MINGW_PREFIX}/bin/ninja.exe install
}
