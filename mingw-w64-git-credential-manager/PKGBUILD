
_realname="git-credential-manager"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
replaces=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
pkgver=2.6.1
# pkgrel should be larger than the official one
pkgrel=3
_realver=$pkgver
_realtag=v${pkgver}
pkgdesc="Credential Manager for Git"
install=git-credential-manager.install
arch=('any')
project_url="https://github.com/git-ecosystem/git-credential-manager"
zip_url="${project_url}/releases/download/${_realtag}/gcm-win-x86-${_realver}.zip"
license=('MIT')
options=('!strip')

source=("${zip_url}")

sha256sums=('bddbc4a051fbfa67d70cd2b89d49229fc1a7788f8a9296da914fb3c42b783d7e')

package() {
    prefix="$pkgdir/${MINGW_PREFIX}"
    install -d -m755 "${prefix}"/libexec/git-core
    install -m755 "$srcdir"/*.{dll,exe,config} "${prefix}"/libexec/git-core/
    install -d -m755 "${prefix}"/share/licenses/git-credential-manager
    install -m644 "$srcdir"/NOTICE "${prefix}"/share/licenses/git-credential-manager
}
