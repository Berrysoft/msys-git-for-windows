
_realname="git-credential-manager"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
replaces=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
pkgver=2.7.1
# pkgrel should be larger than the official one
pkgrel=2
_realver=$pkgver
_realtag=v${pkgver}
pkgdesc="Credential Manager for Git"
install=git-credential-manager.install
arch=('any')
project_url="https://github.com/git-ecosystem/git-credential-manager"

case "$CARCH" in
i686)
    DOTNET_ARCH=x86
    PCHKSUM=ef0bb67678a8ef20a071375a810bd9ad9a4806c75c1f22d09033daa786dcf446
    ;;
x86_64)
    DOTNET_ARCH=x64
    PCHKSUM=86369d14f0d90e8c5bac4fd829ded47ee1a80a7fdad8cb76ba1938d3d7320f7e
    ;;
aarch64)
    DOTNET_ARCH=arm64
    PCHKSUM=93e5cc57bcb7b5b5fe6614397d99e2b17b017af64c2a8b543453b02a90ee821c
    ;;
esac

zip_url="${project_url}/releases/download/${_realtag}/gcm-win-${DOTNET_ARCH}-${_realver}.zip"
license=('MIT')
options=('!strip')

source=("${zip_url}")

sha256sums=("${PCHKSUM}")

package() {
    prefix="$pkgdir/${MINGW_PREFIX}"
    install -d -m755 "${prefix}"/libexec/git-core
    install -m755 "$srcdir"/*.{dll,exe,config} "${prefix}"/libexec/git-core/
    install -d -m755 "${prefix}"/share/licenses/git-credential-manager
    install -m644 "$srcdir"/NOTICE "${prefix}"/share/licenses/git-credential-manager
}
