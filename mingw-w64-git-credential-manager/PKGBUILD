
_realname="git-credential-manager"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
replaces=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
pkgver=2.7.2
# pkgrel should be larger than the official one
pkgrel=2
_realver=$pkgver
_realtag=v${pkgver}
pkgdesc="Credential Manager for Git"
install=git-credential-manager.install
arch=('any')
project_url="https://github.com/git-ecosystem/git-credential-manager"

case "$CARCH" in
i686)
    DOTNET_ARCH=x86
    PCHKSUM=4010820705b193c010bdee8a661ae66bea6e01a33952ca14bed7485217660fc9
    ;;
x86_64)
    DOTNET_ARCH=x64
    PCHKSUM=6674e105c10a778d9011670d7829182030f8401142357339880d0d4aec622d01
    ;;
aarch64)
    DOTNET_ARCH=arm64
    PCHKSUM=c4cf90ae5d3a1cff10cbc3670df6251daa58ee8c711b056e821347b3f22fbc5a
    ;;
esac

zip_url="${project_url}/releases/download/${_realtag}/gcm-win-${DOTNET_ARCH}-${_realver}.zip"
license=('MIT')
options=('!strip')

source=("${zip_url}")

sha256sums=("${PCHKSUM}")

package() {
    prefix="$pkgdir/${MINGW_PREFIX}"
    install -d -m755 "${prefix}"/libexec/git-core
    install -m755 "$srcdir"/*.{dll,exe,config} "${prefix}"/libexec/git-core/
    install -d -m755 "${prefix}"/share/licenses/git-credential-manager
    install -m644 "$srcdir"/NOTICE "${prefix}"/share/licenses/git-credential-manager
}
