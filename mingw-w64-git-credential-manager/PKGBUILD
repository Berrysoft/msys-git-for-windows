
_realname="git-credential-manager"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
replaces=("${MINGW_PACKAGE_PREFIX}-git-credential-manager-core")
pkgver=2.7.0
# pkgrel should be larger than the official one
pkgrel=2
_realver=$pkgver
_realtag=v${pkgver}
pkgdesc="Credential Manager for Git"
install=git-credential-manager.install
arch=('any')
project_url="https://github.com/git-ecosystem/git-credential-manager"

case "$CARCH" in
i686)
    DOTNET_ARCH=x86
    PCHKSUM=bbed7b0f56517f92825f4a6d2f98cae9757da6455c977f93d24745fceb48b806
    ;;
x86_64)
    DOTNET_ARCH=x64
    PCHKSUM=070c7cf706fbed844757f53d2f9d46ace09745820323264761e4f0bb4f0319bc
    ;;
aarch64)
    DOTNET_ARCH=arm64
    PCHKSUM=2b69098b1d772bf6c3cc70298bfa29d0ecf4b4f61ec86f810fd1121e79f81721
    ;;
esac

zip_url="${project_url}/releases/download/${_realtag}/gcm-win-${DOTNET_ARCH}-${_realver}.zip"
license=('MIT')
options=('!strip')

source=("${zip_url}")

sha256sums=("${PCHKSUM}")

package() {
    prefix="$pkgdir/${MINGW_PREFIX}"
    install -d -m755 "${prefix}"/libexec/git-core
    install -m755 "$srcdir"/*.{dll,exe,config} "${prefix}"/libexec/git-core/
    install -d -m755 "${prefix}"/share/licenses/git-credential-manager
    install -m644 "$srcdir"/NOTICE "${prefix}"/share/licenses/git-credential-manager
}
