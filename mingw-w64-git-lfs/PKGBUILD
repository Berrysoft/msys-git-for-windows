# Maintainer: Berrysoft <Strawberry_Str@hotail.com>

_realname="git-lfs"
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=3.1.4
# pkgrel should be larger than the official one
pkgrel=3
pkgdesc="An open source Git extension for versioning large files"
arch=('any')
url="https://github.com/git-lfs/git-lfs"
license=('MIT')
options=('!strip')
groups=('VCS')

makedepends=("git"
             "make"
             "${MINGW_PACKAGE_PREFIX}-go")

source=("git"::"git+https://github.com/git-lfs/git-lfs#tag=v$pkgver")

sha256sums=('SKIP')

case "$CARCH" in
i686)
    GOARCH=386
    ;;
x86_64)
    GOARCH=amd64
    ;;
aarch64)
    GOARCH=arm64
    ;;
esac

prepare() {
    mkdir -p "$srcdir/go"
    GOPATH="$srcdir/go" ${MINGW_PREFIX}/bin/go install github.com/josephspurrier/goversioninfo/cmd/goversioninfo@latest
}

build() {
    cd "$srcdir/git"
    LDFLAGS="$(echo "$LDFLAGS" | sed 's/-Wl,--no-seh//')"
    GOPATH="$srcdir/go" PATH="$PATH:$srcdir/go/bin" make bin/git-lfs-windows-${GOARCH}.exe
}

package() {
    install -d -m755 $pkgdir/$MINGW_PREFIX/bin
    install -m755 $srcdir/git/bin/git-lfs-windows-${GOARCH}.exe $pkgdir/$MINGW_PREFIX/bin/git-lfs.exe
    install -d -m755 $pkgdir/$MINGW_PREFIX/share/doc/git-lfs
    install -m755 $srcdir/git/README.md $pkgdir/$MINGW_PREFIX/share/doc/git-lfs/README.md
    install -Dm644 $srcdir/git/LICENSE.md "${pkgdir}${MINGW_PREFIX}"/share/licenses/git-lfs/LICENSE
}
