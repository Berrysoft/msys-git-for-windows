
_realname=git-warp-time
pkgbase="mingw-w64-${_realname}"
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0.8.4
pkgrel=1
pkgdesc='reset timestamps of Git repository files to the time of the last modifying commit'
arch=('any')
url="https://github.com/alerque/git-warp-time"
license=(GPL3)

makedepends=("diffutils"
             "make"
             "${MINGW_PACKAGE_PREFIX}-autotools"
             "${MINGW_PACKAGE_PREFIX}-jq"
             "${MINGW_PACKAGE_PREFIX}-pkgconf"
             "${MINGW_PACKAGE_PREFIX}-rust")

depends=("${MINGW_PACKAGE_PREFIX}-libgit2")

source=("https://github.com/alerque/git-warp-time/releases/download/v$pkgver/git-warp-time-$pkgver.tar.zst")
sha256sums=('5e1df49c0efb709efe51f2a522c967c83fbddc0b6743ef0fdec085b06d030149')

options=('strip')

build() {
    cd "$srcdir/git-warp-time-$pkgver"
    echo "$pkgver.Berrysoft.$pkgrel" > .tarball-version
    ./configure --prefix $MINGW_PREFIX
    make
}

check() {
    cd "$srcdir/git-warp-time-$pkgver"
    make check
}

package() {
    cd "$srcdir/git-warp-time-$pkgver"
    make DESTDIR="$pkgdir" bindir="$MINGW_PREFIX/libexec/git-core" install
}
